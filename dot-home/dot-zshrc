# ZSH interactive shell setup

# Also see the following files:
#
# .zshenv   | sources .profile
# .zprofile | not used
# .zshrc    | interactive shell setup
# .zlogin   | not used
# .zlogout  | not used

# Load prompt.
setopt PROMPT_SUBST

git_prompt() {
    local git_branch=$(git --no-optional-locks branch --show-current 2> /dev/null)
    if [[ -n "$git_branch" ]]; then
        local git_status=$(git --no-optional-locks status --porcelain 2> /dev/null | tail -n 1)
        [[ -n "$git_status" ]] && echo -n "%F{yellow}" || echo -n "%F{green}"
        echo -n "‹${git_branch}›%f"
    fi
}

export PROMPT=$'%F{#6272a4}%~%f $(git_prompt)\n%(?.%F{green}.%F{red})❯%f '

# Source aliases and functions.
source "$HOME/.aliases"
source "$HOME/.functions"

# Setup history.
export HISTFILE="$HOME/.zsh_history"
export HISTSIZE=1000000 # size of the loaded history (memory)
export SAVEHIST=1000000 # size of the history file
setopt SHARE_HISTORY # sync between sessions
setopt HIST_IGNORE_ALL_DUPS # do not save dublicates

# Use VIM as default editor...
if [[ -n $SSH_CONNECTION ]]; then
  export EDITOR='vim'
else
  export EDITOR='nvim'
fi

# ...but still emacs keybinds in ZSH.
bindkey -e

# fast-theme XDG:overlay

# Load additional completions.
fpath=("$HOME/.local/share/zsh-completions" $fpath)

# Initialize compinit.
autoload -Uz compinit 
case $(uname -s) in
  Darwin)
    if [ $(date +'%j') != $(/usr/bin/stat -f '%Sm' -t '%j' ${ZDOTDIR:-$HOME}/.zcompdump) ]; then
      compinit;
    else
      compinit -C;
    fi
    ;;
  Linux)
    if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
        compinit
      else
        compinit -C
    fi
    ;;
esac

# Load fast-syntax-highlighting.
source "$HOME/.local/share/fsh/fast-syntax-highlighting.plugin.zsh"

# On WSL, go to linux home.
if [[ $(grep Microsoft /proc/version) ]]; then
    cd "$HOME"
fi

# Setup FZF.

# ctrl-t: Fuzzy search paths under current dir.
# ctrl-r: Fuzzy search history (takes partial line into account).
# alt-c: Similar to ctrl-t, but includes cd.
source /usr/share/doc/fzf/examples/key-bindings.zsh
source /usr/share/doc/fzf/examples/completion.zsh

# Set the FZF color theme to dracula.
export FZF_DEFAULT_OPTS=" \
  --color=fg:#f8f8f2,bg:#282a36,hl:#bd93f9 \
  --color=fg+:#f8f8f2,bg+:#44475a,hl+:#bd93f9 \
  --color=info:#ffb86c,prompt:#50fa7b,pointer:#ff79c6 \
  --color=marker:#ff79c6,spinner:#ffb86c,header:#6272a4"

# Initialize zoxide (z).
eval "$(zoxide init zsh)"

